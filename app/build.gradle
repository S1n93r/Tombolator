apply plugin: 'com.android.application'

android {
    namespace 'com.example.tombolator'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.tombolator"
        minSdk 30
        targetSdk 33

        def versionCode_ = generateVersionCode()

        versionCode = versionCode_
        versionName = generateVersionName(versionCode_)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            '/src/main/res',
                            '/src/main/res/layouts',
                            '/src/main/res/layouts/commons',
                            '/src/main/res/layouts/config',
                            '/src/main/res/layouts/media',
                            '/src/main/res/layouts/tombolas',
                            '/src/main/res/drawables',
                            '/src/main/res/drawables/components',
                            '/src/main/res/drawables/icons'
                    ]
        }
    }
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-fragment:2.7.5'
    implementation 'androidx.navigation:navigation-ui:2.7.5'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // https://projectlombok.org/setup/android
//    compileOnly 'org.projectlombok:lombok:1.18.22'
//    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    // https://mvnrepository.com/artifact/androidx.lifecycle/lifecycle-extensions
    runtimeOnly group: 'androidx.lifecycle', name: 'lifecycle-extensions', version: '2.2.0'

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.5.0'

    // https://mvnrepository.com/artifact/androidx.room/room-runtime
    //implementation group: 'androidx.room', name: 'room-runtime', version: '2.2.6'

    implementation "androidx.room:room-runtime:2.6.0"
    annotationProcessor "androidx.room:room-compiler:2.6.0"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'

    // https://mvnrepository.com/artifact/com.google.guava/guava
    implementation group: 'com.google.guava', name: 'guava', version: '30.1-android'

    implementation group: 'net.sourceforge.streamsupport', name: 'streamsupport', version: '1.7.4'
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

static int generateVersionCode() {

    def revisions = "git rev-list HEAD --count".execute().text.trim() //unix
    if (revisions.empty) revisions = "PowerShell -Command git rev-list HEAD --count".execute().text.trim()
    //windows
    if (revisions.empty) throw new RuntimeException("Could not generate versioncode on this platform? Cmd output: ${revisions.text}")

    return revisions.toInteger()
}

static String generateVersionName(int versionCode) {

    String versionName = "0"

    for (char digit : versionCode.toString().toCharArray()) {
        versionName += "." + digit
    }

    return versionName
}